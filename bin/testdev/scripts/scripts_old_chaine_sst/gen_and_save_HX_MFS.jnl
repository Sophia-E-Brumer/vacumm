	Set Memory /Size=250000
	
	cancel d /all
        !-- Lecture des fichiers
        !   ####################
	Use "SST/SST_NAR.nc"       ! Fichier observations
	Use "SST/SST_prev_mfs.nc"      ! Fichier previsions modele

	!-- Selection obs selon niveau de confiance et prev selon mval et valeur min
	!   ##########################################################################################
        Let /Bad=999 obs0   = If ( conf_level[d=1] ge 3 ) then temp[d=1] else 999
	Let /Bad=999 prev0  = If ( votemper[ d=2 ] ne 99 AND votemper[ d=2 ] gt 0 ) then votemper[ d=2 ] else 999
	
	!-- Definition nouvelles grilles
	!   ############################
	Define Axis /X xnew = x[ d=1, gx=temp ] ! grille x,y = celle des obs
	Define Axis /Y ynew = y[ d=1, gy=temp ]	
	Define Axis /T="24-JUN-2007 00:00:00":"30-JUN-2007 00:00:00":"24" tnew		
	Define Grid /X=xnew /Y=ynew /T=tnew gridnew1

	!-- Grille curviligne en grille rectangulaire
	!   ##############################################
	! For convenience define variables with the input grid 
	let lonin = NAV_LON[d=2]
	let latin = NAV_LAT[d=2]
	! Define output grid and a variable on the output grid 
	let lonlatout = y[gy=ynew] + x[gx=xnew]
	! Compute the mapping to the rectangular grid
	Let mapp = curv_to_rect_map ( lonin,latin,lonlatout,0.2 )
	
	! Apply the mapping to the data fields 
	Let prev01 = curv_to_rect(prev0[k=1], mapp)
		
	!-- Projection sur grille commune
	!   #############################
	
        Let obs1  = obs0[  g=gridnew1 ]
	Let prev1 = prev01[ g=gridnew1 ]
	
	!-- Conserve les points ou obs ET modele ont des donnees valides
	!   ############################################################
	Let /Bad=999  obs2   = IF ( prev1  ne 999 ) then obs1  else 999
	Let /Bad=999  prev2  = IF ( obs2   ne 999 ) then prev1 else 999
	
        Let /Bad=999 /Title="sst_nar"  /unit="degC" sst_nar  = obs2 - 0
	Let /Bad=999 /Title="innov_nar" /unit="degC" innov_nar = sst_nar - prev2

	!-- Sauvegarde ds fichier
	!   #####################
	Save /Clobber /File="/home10/tome/previmer/ASST/RD-PRECOC/WP3/SCRIPTS/SST/NARmfs_colloc.nc" sst_nar
	Save /Append  /File="/home10/tome/previmer/ASST/RD-PRECOC/WP3/SCRIPTS/SST/NARmfs_colloc.nc" innov_nar
